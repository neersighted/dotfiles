# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

pkgname=gvim-neersighted
pkgver=8.0.0027.r0.gdc0ccae
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
url='http://www.vim.org/'
license=('custom:vim')
groups=('editors')
source=('git+https://github.com/vim/vim.git'
        'gvim.desktop')
sha256sums=('SKIP'
            '46dff79072746c2199b8d6ab77936ee72beb4abea83a4c03a7b9174379e8e56a')
arch=('i686' 'x86_64')
depends=('gtk2' 'gdk-pixbuf2' 'pango' 'glib2' 'libsm' 'libice' 'libxt' 'libx11' 'glibc' 'ncurses' 'acl' 'lua' 'perl' 'python' 'python2' 'ruby')
makedepends=('git')
conflicts=('vi' 'vim' 'gvim' 'vim-runtime')
provides=('vi' 'vim' 'gvim' 'vim-runtime')

pkgver() {
  cd vim

  # Get the version number.
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd vim

  ./configure \
    --prefix=/usr \
    --localstatedir=/var/lib/vim \
    --with-features=huge \
    --with-tlib=ncursesw \
    --with-x=yes \
    --enable-multibyte \
    --enable-acl \
    --enable-cscope \
    --disable-netbeans \
    --disable-gpm \
    --enable-gui=gtk3 \
    --enable-rubyinterp \
    --enable-pythoninterp \
    --enable-python3interp \
    --enable-perlinterp \
    --enable-luainterp \
    --enable-fail-if-missing
  make
}

package() {
  cd vim

  # Install the program.
  make DESTDIR=${pkgdir} install

  ln -s /usr/bin/vim ${pkgdir}/usr/bin/vi

  # Install the XDG files.
  install -Dm644 ${srcdir}/gvim.desktop ${pkgdir}/usr/share/applications/gvim.desktop
  install -Dm644 runtime/vim48x48.png ${pkgdir}/usr/share/pixmaps/gvim.png

  # Install the license.
  install -Dm644 runtime/doc/uganda.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
