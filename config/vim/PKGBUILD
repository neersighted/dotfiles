pkgname=gvim-neersighted
pkgver=latest
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
url='http://www.vim.org/'
license=('custom:vim')
groups=('editors')
source=('hg+http://vim.googlecode.com/hg/vim' 'gvim.desktop')
md5sums=('SKIP' '6e11c556ba3f2ce7dc05d9908188d604')
arch=('i686' 'x86_64')
depends=('gtk2' 'gdk-pixbuf2' 'pango' 'glib2' 'libsm' 'libice' 'libxt' 'libx11' 'glibc' 'ncurses' 'acl' 'lua' 'perl' 'python')
makedepends=('mercurial')
conflicts=('vim' 'gvim' 'vim-runtime')
provides=('vim' 'gvim' 'vim-runtime')

pkgver() {
    cd vim

    # OH GOD WHY.
    hg tip --template '{latesttag}\n' | sed 's/^v//;s/-/./g'
}

build() {
    cd vim

    ./configure \
        --prefix=/usr \
        --localstatedir=/var/lib/vim \
        --with-x=yes \
        --enable-gui=gtk2 \
        --enable-rubyinterp \
        --disable-pythoninterp \
        --enable-python3interp \
        --enable-perlinterp \
        --enable-luainterp \
        --enable-acl \
        --enable-multibyte \
        --enable-cscope \
        --disable-netbeans \
        --disable-gpm \
        --with-features=huge \
        --enable-fail-if-missing \
        --with-compiledby="neersighted"
    make
}

check() {
    cd vim

    # Run the tests.
    make test
}

package() {
    cd vim

    # Install the program.
    make DESTDIR=${pkgdir} install

    # These are provided by (n)vi.
    rm ${pkgdir}/usr/bin/ex
    rm ${pkgdir}/usr/bin/view
    shopt -s globstar
        rm ${pkgdir}/usr/share/man/**/ex.1*
        rm ${pkgdir}/usr/share/man/**/view.1*
    shopt -u globstar

    # Install the XDG files.
    install -Dm644 ${srcdir}/gvim.desktop ${pkgdir}/usr/share/applications/gvim.desktop
    install -Dm644 runtime/vim48x48.png ${pkgdir}/usr/share/pixmaps/gvim.png

    # Install the license.
    install -Dm644 runtime/doc/uganda.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
