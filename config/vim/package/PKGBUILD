pkgname=gvim-neersighted
pkgver=7.4.335.r1.4aa63564dd3f
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
url='http://www.vim.org/'
license=('custom:vim')
groups=('editors')
source=('vim::hg+https://vim.googlecode.com/hg/' 'gvim.desktop')
md5sums=('SKIP' '6e11c556ba3f2ce7dc05d9908188d604')
arch=('i686' 'x86_64')
depends=('gtk2' 'gdk-pixbuf2' 'pango' 'glib2' 'libsm' 'libice' 'libxt' 'libx11' 'glibc' 'ncurses' 'acl' 'lua' 'perl' 'python')
makedepends=('mercurial')
conflicts=('vim' 'gvim' 'vim-runtime')
provides=('vim' 'gvim' 'vim-runtime')

pkgver() {
    cd vim

    # OH GOD WHY.
    hg log -r "." --template "{sub('-', '.', strip(latesttag, 'v'))}.r{latesttagdistance}.{node|short}"
}

build() {
    cd vim

    ./configure \
        --enable-acl \
        --disable-gpm \
        --with-x=yes \
        --prefix=/usr \
        --enable-cscope \
        --enable-gui=gtk2 \
        --enable-luainterp \
        --enable-multibyte \
        --disable-netbeans \
        --enable-perlinterp \
        --enable-rubyinterp \
        --with-features=huge \
        --disable-pythoninterp \
        --enable-python3interp \
        --localstatedir=/var/lib/vim
    make
}

check() {
    cd vim

    # Run the tests.
    make test
}

package() {
    cd vim

    # Install the program.
    make DESTDIR=${pkgdir} install

    # These are provided by (n)vi.
    rm ${pkgdir}/usr/bin/ex
    rm ${pkgdir}/usr/bin/view
    shopt -s globstar
        rm ${pkgdir}/usr/share/man/**/ex.1*
        rm ${pkgdir}/usr/share/man/**/view.1*
    shopt -u globstar

    # Install the XDG files.
    install -Dm644 ${srcdir}/gvim.desktop ${pkgdir}/usr/share/applications/gvim.desktop
    install -Dm644 runtime/vim48x48.png ${pkgdir}/usr/share/pixmaps/gvim.png

    # Install the license.
    install -Dm644 runtime/doc/uganda.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
