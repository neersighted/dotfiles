# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

_pkgname=slock
pkgname=slock-neersighted
pkgver=1.3.28.gbd069b0
pkgrel=1
pkgdesc='Simple screen locker'
url='http://tools.suckless.org/slock'
license=('MIT')
source=('git://git.suckless.org/slock'
        'config.h')
sha256sums=('SKIP'
            'SKIP')
arch=('i686' 'x86_64')
depends=('libxext')
conflicts=('slock')
provides=('slock')

pkgver() {
  cd "${srcdir}/slock"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/slock"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/slock"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
