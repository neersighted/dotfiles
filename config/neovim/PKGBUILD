# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

_pkgname=neovim
pkgname=neovim-neersighted
pkgver=0.1.5.r201.g4408431
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs.'
url='https://neovim.io'
license=('custom:neovim')
groups=('editors')
source=("${_pkgname}::git+https://github.com/neovim/neovim.git")
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=('jemalloc' 'libtermkey' 'libuv' 'libvterm' 'msgpack-c' 'unibilium')
makedepends=('cmake' 'git' 'gperf' 'luajit' 'lua51-mpack' 'lua51-lpeg')
optdepends=('python2-neovim: for Python 2 plugin support (see :help provider-python)'
            'python-neovim: for Python 3 plugin support (see :help provider-python)'
            'ruby-neovim: for Ruby plugin support (see :help provider-ruby)'
            'xclip: for clipboard support (or xsel) (see :help provider-clipboard)'
            'xsel: for clipboard support (or xclip) (see :help provider-clipboard)')
conflicts=('neovim')
provides=('neovim')

pkgver() {
  cd ${_pkgname}

  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  mkdir -p ${_pkgname}/build
  cd ${_pkgname}/build

  cmake -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=Dev \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_JEMALLOC=ON \
    ..
  make
}

check() {
  cd ${_pkgname}/build

  ./bin/nvim --version
  ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
  cd ${_pkgname}/build

  # Install the program.
  make DESTDIR=${pkgdir} install

  # Install the license.
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set sw=2 sts=2 et:
