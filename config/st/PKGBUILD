# Maintainer: neersighted <n@neersighted.com>

_pkgname=st
pkgname=st-neersighted
pkgver=b331da5
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X'
url='http://st.suckless.org'
license=('MIT')
source=('git://git.suckless.org/st'
        'config.h'
        'login.diff')
sha256sums=('SKIP'
            'e9dc38f6fcec28bd20a3090e53adb8fb70c5840977d85f9710108a9f4426c150'
            'c9d3fcafa93f6bd04618ef32594eebef72f2a2f1deb9d28ebf82937e59651903')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('st')
provides=('st')

pkgver() {
  cd "${srcdir}/st"

  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cd "${srcdir}/st"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Apply patches.
  git apply "${srcdir}/login.diff"

  # Skip terminfo (conflict).
  sed -i '/\@tic /d' Makefile
}

build() {
  cd "${srcdir}/st"

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/st"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
