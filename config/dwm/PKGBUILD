# Maintainer: neersighted <n@neersighted.com>

pkgname=dwm-neersighted
pkgver=35db6d8
pkgrel=1
pkgdesc='The dynamic window manager'
url='http://dwm.suckless.org'
license=('MIT')
source=('git://git.suckless.org/dwm'
        'config.h'
        'xft.diff')
md5sums=('SKIP'
         'SKIP'
         '0e55dddc40fae284035a0a049e505fbe')
sha1sums=('SKIP'
          'SKIP'
          '4da5e10f560c2e18b77a147089561e765569fba9')
sha256sums=('SKIP'
            'SKIP'
            '40e09e391e7656256b621b640b5f75e2640fa2a2ba7fb2f3421ca45a49768632')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('dwm')
provides=('dwm')

pkgver() {
  cd "${srcdir}/dwm"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dwm"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Apply patches.
  git apply "${srcdir}/xft.diff"

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dwm"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
