# Maintainer: neersighted <n@neersighted.com>

pkgname=dwm-neersighted
pkgver=35db6d8
pkgrel=1
pkgdesc='The dynamic window manager'
url='http://dwm.suckless.org'
license=('MIT')
source=('git://git.suckless.org/dwm'
        'config.h'
        'multistatus.diff'
        'restart.diff'
        'smartborders.diff'
        'xft.diff')
md5sums=('SKIP'
         'SKIP'
         'b2960a40e54f0dba070b0a2db9ae8f25'
         'f52d3f16ddde2ecd34458354eca5c783'
         '62505662ecdfcc444b636353d53d0ca7'
         '0e55dddc40fae284035a0a049e505fbe')
sha1sums=('SKIP'
          'SKIP'
          '70a39f36defb71cc404c197b06c071eba575deb4'
          '69cae9bcfcfde8af806bc87c708072bbf5841556'
          '583437c50c4d04d39f2f8b0c466f0b572753eada'
          '4da5e10f560c2e18b77a147089561e765569fba9')
sha256sums=('SKIP'
            'SKIP'
            '99863ea1e9843aedb569430b018da68b2af4f9fe7315506e7c3de977baabeeef'
            'a3e528e71df55a904e90205963522d20c3a8091e9ff38e7b49051ee942b94e27'
            '8c0e6376fe5a96c0dc6a897635cc00e600ffa2c073a306a2e9129c5522397ab2'
            '40e09e391e7656256b621b640b5f75e2640fa2a2ba7fb2f3421ca45a49768632')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('dwm')
provides=('dwm')

pkgver() {
  cd "${srcdir}/dwm"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dwm"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Apply patches.
  git apply "${srcdir}/multistatus.diff"
  git apply "${srcdir}/restart.diff"
  git apply "${srcdir}/smartborders.diff"
  git apply "${srcdir}/xft.diff"

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dwm"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
