# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

_pkgname=dwm
pkgname=dwm-neersighted
pkgver=ceac8c9
pkgrel=1
pkgdesc='The dynamic window manager'
url='http://dwm.suckless.org'
license=('MIT')
source=('git://git.suckless.org/dwm'
        'config.h'
        'btile.c'
        'cyclelayout.c'
        'gaplessgrid.c'
        'push.c'
        'restart.diff'
        'smartborders.diff')
sha256sums=('SKIP'
            '83d9ddc570da56f85da9e728ec6afffa87b2c88dd581f50ffb3f9864f2dd8edb'
            '747b2fcf1fcb62370e4748e93da1884cfa1b66b636078bedd9730a0f1b053ecd'
            '7a0bf96614ff56637ebd3a062fd5fe967e5485bccfa1d24ea8d68ab499c9402f'
            'd1b8559b3ba36fd36c2bfe9928a80df2458e0136e52ffb0384f0f05f0263a58a'
            '195c8c9f2c0af9f862b398a306bb1a791931beaf541e37df41f13cb44438301c'
            '1458ec1007f39573486dd7c01ebf021d87c82e0e45cac86acfa69ba8fd89d69c'
            'd11937763dcf3cc09e1324c0d31473789a5db0b75cfb32ac709b3a71a1330961')
arch=('i686' 'x86_64')
depends=('libxft' 'libxinerama')
makedepends=('git' 'ncurses')
conflicts=('dwm')
provides=('dwm')

pkgver() {
  cd "${srcdir}/dwm"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dwm"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h
  cp "${srcdir}/btile.c" ./btile.c
  cp "${srcdir}/cyclelayout.c" ./cyclelayout.c
  cp "${srcdir}/gaplessgrid.c" ./gaplessgrid.c
  cp "${srcdir}/push.c" ./push.c

  # Apply patches.
  git apply "${srcdir}/restart.diff"
  git apply "${srcdir}/smartborders.diff"

  # Un-hard-code flags.
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
  sed -i 's/^CC =/#CC =/g' config.mk

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dwm"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
