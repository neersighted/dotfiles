# Maintainer: neersighted <n@neersighted.com>

pkgname=dwm-neersighted
pkgver=35db6d8
pkgrel=1
pkgdesc='The dynamic window manager'
url='http://dwm.suckless.org'
license=('MIT')
source=('git://git.suckless.org/dwm'
        'config.h'
        'push.c'
        'adjtag.c'
        'multistatus.diff'
        'pertag-tab.diff'
        'restart.diff'
        'smartborders.diff'
        'xft.diff')
sha256sums=('SKIP'
            'SKIP'
            '195c8c9f2c0af9f862b398a306bb1a791931beaf541e37df41f13cb44438301c'
            '0aea10231f2b82b8c0364daf1fe950101873a5e4aa4d23c536ef228810e4f72d'
            '99863ea1e9843aedb569430b018da68b2af4f9fe7315506e7c3de977baabeeef'
            '81b19bbebcddd557f6100027e248308918535ba3f9e772b6690f50676e8c9e0b'
            'a3e528e71df55a904e90205963522d20c3a8091e9ff38e7b49051ee942b94e27'
            '8c0e6376fe5a96c0dc6a897635cc00e600ffa2c073a306a2e9129c5522397ab2'
            '40e09e391e7656256b621b640b5f75e2640fa2a2ba7fb2f3421ca45a49768632')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('dwm')
provides=('dwm')

pkgver() {
  cd "${srcdir}/dwm"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dwm"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h
  cp "${srcdir}/adjtag.c" ./adjtag.c
  cp "${srcdir}/push.c" ./push.c

  # Apply patches.
  git apply "${srcdir}/multistatus.diff"
  git apply "${srcdir}/pertag-tab.diff"
  git apply "${srcdir}/restart.diff"
  git apply "${srcdir}/smartborders.diff"
  git apply "${srcdir}/xft.diff"

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dwm"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
