# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

_pkgname=dmenu
pkgname=dmenu-neersighted
pkgver=44b242c
pkgrel=1
pkgdesc='A dynamic menu for X'
url='http://tools.suckless.org/dmenu'
license=('MIT')
source=('git://git.suckless.org/dmenu'
        'config.h')
sha256sums=('SKIP'
            'SKIP')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('dmenu')
provides=('dmenu')

pkgver() {
  cd "${srcdir}/dmenu"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dmenu"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dmenu"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
