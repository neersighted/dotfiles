# Maintainer: neersighted <n@neersighted.com>

pkgname=dmenu-neersighted
pkgver=13a529c
pkgrel=1
pkgdesc='The dynamic window manager'
url='http://tools.suckless.org/dmenu'
license=('MIT')
source=('git://git.suckless.org/dmenu'
        'config.h'
        'xft.diff')
sha256sums=('SKIP'
            'SKIP'
            'a27b475bfa124ce4b4b45fd8047de0948ed466e1fe63c3223417305ea3016a8b')
arch=('i686' 'x86_64')
depends=('libxext' 'libxft')
makedepends=('ncurses')
conflicts=('dmenu')
provides=('dmenu')

pkgver() {
  cd "${srcdir}/dmenu"

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/dmenu"

  # Apply config.
  cp "${srcdir}/config.h" ./config.h

  # Apply patches.
  git apply "${srcdir}/xft.diff"

  # Build it!
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/dmenu"

  # Install the program.
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install

  # Install the license.
  install -Dm644 LICENSE \
                    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install the readme.
  install -Dm644 README \
                    "${pkgdir}/usr/share/doc/${pkgname}/README"
}
