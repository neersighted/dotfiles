#!/usr/bin/env fish

set_color yellow
echo "Updating nodenv..."
set_color normal

for repo in $NODENV_ROOT $NODENV_ROOT/plugins/*
  git -C $repo pull -q
end

set_color green
echo "Updating packages..."
set_color normal

function npm
  command nodenv exec npm $argv
end

for node in (nodenv versions --bare)
  set -lx NODENV_VERSION $node

  for spec in (npm outdated -g --json | jq -c 'to_entries[]')
    set name (jq --argjson spec $spec -nr '$spec | "\(.key)"')
    set current (jq --argjson spec $spec -nr '$spec | "\(.value.current)"')
    set latest (jq --argjson spec $spec -nr '$spec | "\(.value.latest)"')

    set_color green
    echo "Updating $name (Node.js $node)..."
    set_color normal

    npm install -g "$name@$latest"
  end
end
