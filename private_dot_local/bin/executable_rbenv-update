#!/usr/bin/env fish

set_color red
echo "Updating rbenv..."
set_color normal

for repo in $RBENV_ROOT $RBENV_ROOT/plugins/*
  git -C $repo pull -q
end

set_color red
echo "Updating gems..."
set_color normal

function gem
  command rbenv exec gem $argv
end

set latest_rubygems (curl -sS https://rubygems.org/api/v1/gems/rubygems-update.json | jq -r '.version')
for ruby in (rbenv versions --bare)
  set -lx RBENV_VERSION $ruby

  set local_rubygems (gem --version)
  if test "$local_rubygems" != "$latest_rubygems"
    set_color red
    echo "Updating RubyGems from $local_rubygems to $latest_rubygems ($ruby)..."
    set_color normal

    gem update --system
  end

  for gem in (gem outdated)
    string match -rq '(?<name>.+) \((?<old>.+) < (?<new>.+)\)' $gem

    set_color red
    echo "Updating $name from $old to $new ($ruby)..."
    set_color normal

    gem update $name
  end
end

chezmoi apply
