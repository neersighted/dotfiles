#!/usr/bin/env fish

set_color yellow
echo "Updating pyenv..."
set_color normal

git -C $PYENV_ROOT pull -q

set_color yellow
echo "Updating Poetry..."
set_color normal

poetry self update

set_color yellow
echo "Updating pipx venvs..."
set_color normal

pipx upgrade-all

set_color yellow
echo "Updating packages..."
set_color normal

# allow running pip on global installations
set -eg PIP_REQUIRE_VIRTUALENV

function pip
  command pyenv exec python -m pip $argv
end

for python in (pyenv versions --bare --skip-aliases)
  set -lx PYENV_VERSION $python

  for spec in (pip list --outdated --format=json | jq -c '.[]')
    set name (jq --argjson spec $spec -nr '$spec | "\(.name)"')
    set current (jq --argjson spec $spec -nr '$spec | "\(.version)"')
    set latest (jq --argjson spec $spec -nr '$spec | "\(.latest_version)"')

    set_color yellow
    echo "Updating $name from $current to $latest ($python)..."
    set_color normal

    pip install -U $name
  end
end

chezmoi apply
