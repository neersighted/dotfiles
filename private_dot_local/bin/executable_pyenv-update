#!/usr/bin/env fish

set_color yellow
echo "Updating pyenv..."
set_color normal

for repo in $PYENV_ROOT $PYENV_ROOT/plugins/*
  git -C $repo pull -q
end

set latest (pyenv latest -p)
set installed (pyenv latest -P)

if test "$installed" != "$latest"
  set_color yellow
  echo "Installing latest Python..."
  set_color normal

  pyenv latest install
end

set_color yellow
echo "Updating system packages..."
set_color normal

# allow running pip on global installations
set -eg PIP_REQUIRE_VIRTUALENV

function pip
  command pyenv exec python -m pip --disable-pip-version-check $argv
end

for python in (pyenv versions --bare --skip-aliases)
  set -lx PYENV_VERSION $python

  for spec in (pip list --outdated --format=json | jq -c '.[]')
    set name (jq --argjson spec $spec -nr '$spec | "\(.name)"')
    set current (jq --argjson spec $spec -nr '$spec | "\(.version)"')
    set latest (jq --argjson spec $spec -nr '$spec | "\(.latest_version)"')

    set_color yellow
    echo "Updating $name from $current to $latest ($python)..."
    set_color normal

    pip install -U $name
  end
end
