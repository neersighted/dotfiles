{{- if lookPath "chezmoi" -}}
function chezmoi
  switch $argv[1]
  case cd
    cd (chezmoi source-path)
  case edit
    if test (count $argv) -eq 1
      if set targets (chezmoi managed --include=files,symlinks,scripts \
        | fzf --multi --preview \
          'chezmoi cat ~/{} | bat --style=numbers --color=always --file-name ~/{}')
        set args '~/'$targets
        commandline --replace "chezmoi edit $args"
      end
    else
      # use the git-editor wrapper to take advantage of custom logic
      set -lx VISUAL git-editor

      command chezmoi $argv
    end
  case add
    if test (count $argv) -eq 1
      if set modified (chezmoi status \
        | string match -re '^.M|^M.' | string sub -s 4 \
        | fzf --multi --preview 'chezmoi diff --reverse --color=true ~/{}')
        set args '~/'$modified
        commandline --replace "chezmoi re-add $args"
      end
    else
      command chezmoi $argv
    end
  case '*'
    command chezmoi $argv
  end
end
{{- end -}}
