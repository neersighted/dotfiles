function fish_color
  set -g $argv[1] $argv[2..-1]
  set -g -a fish_colors $argv[1]
end

# syntax highlighting
fish_color fish_color_normal '{{ .nord.nord4 }}'
fish_color fish_color_command '{{ .nord.nord14 }}'
fish_color fish_color_param '{{ .nord.nord4 }}'
fish_color fish_color_redirection '{{ .nord.nord15 }}'
fish_color fish_color_comment '{{ .nord.nord2 }}'
fish_color fish_color_error '{{ .nord.nord11 }}'
fish_color fish_color_escape '{{ .nord.nord8 }}'
fish_color fish_color_operator '{{ .nord.nord9 }}'
fish_color fish_color_end '{{ .nord.nord3 }}'
fish_color fish_color_quote '{{ .nord.nord4 }}'
fish_color fish_color_autosuggestion '{{ .nord.nord3 }}'
fish_color fish_color_valid_path --underline
fish_color fish_color_match --background '{{ .nord.nord9 }}'
fish_color fish_color_search_match nord13 --background '{{ .nord.nord3 }}'
fish_color fish_color_selection '{{ .nord.nord6 }}' --background '{{ .nord.nord3 }}'
fish_color fish_color_cancel -r

# prompt
fish_color fish_color_cwd '{{ .nord.nord9 }}'
fish_color fish_color_cwd_root $fish_color_cwd
fish_color fish_color_host '{{ .nord.nord15 }}'
fish_color fish_color_host_remote '{{ .nord.nord15 }}'
fish_color fish_color_jobs '{{ .nord.nord12 }}'
fish_color fish_color_status '{{ .nord.nord11 }}'
fish_color fish_color_timer '{{ .nord.nord3 }}'
fish_color fish_color_user '{{ .nord.nord10 }}'
fish_color fish_color_user_root '{{ .nord.nord4 }}'

# version prompt
fish_color prompt_version_color_golang '{{ .nord.nord8 }}'
fish_color prompt_version_color_nodejs '{{ .nord.nord14 }}'
fish_color prompt_version_color_python '{{ .nord.nord13 }}'
fish_color prompt_version_color_python_venv '{{ .nord.nord13 }}'
fish_color prompt_version_color_ruby '{{ .nord.nord11 }}'
fish_color prompt_version_color_rust '{{ .nord.nord12 }}'

# git prompt
fish_color prompt_git_color_branch '{{ .nord.nord10 }}'
fish_color prompt_git_color_detached '{{ .nord.nord15 }}'
fish_color prompt_git_color_upstream '{{ .nord.nord9 }}'
fish_color prompt_git_color_operation '{{ .nord.nord12 }}'
fish_color prompt_git_color_staged '{{ .nord.nord14 }}'
fish_color prompt_git_color_unstaged '{{ .nord.nord8 }}'
fish_color prompt_git_color_unmerged '{{ .nord.nord11 }}'
fish_color prompt_git_color_untracked '{{ .nord.nord13 }}'
fish_color prompt_git_color_stashed '{{ .nord.nord10 }}'

fish_color prompt_git_clean ''

# dirh
fish_color fish_color_history_current --bold

# pager
fish_color fish_pager_color_prefix '{{ .nord.nord3 }}'
fish_color fish_pager_color_completion '{{ .nord.nord4 }}'
fish_color fish_pager_color_description '{{ .nord.nord13 }}'
fish_color fish_pager_color_progress '{{ .nord.nord6 }}' --background '{{ .nord.nord10 }}'

# manpages
fish_color man_color_blink '{{ .nord.nord15 }}' --bold --underline
fish_color man_color_bold '{{ .nord.nord5 }}'
fish_color man_color_standout --reverse
fish_color man_color_underline '{{ .nord.nord10 }}' --underline

functions -e fish_color

# fzf
set -x FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS_BASE \
  --color=fg:'{{ .nord.nord5 }}',bg:'{{ .nord.nord0 }}',hl:'{{ .nord.nord9 }}' \
  --color=fg+:'{{ .nord.nord5 }}',bg+:'{{ .nord.nord1 }}',hl+:'{{ .nord.nord9 }}' \
  --color=info:'{{ .nord.nord13 }}',prompt:'{{ .nord.nord11 }}',pointer:'{{ .nord.nord15 }}' \
  --color=marker:'{{ .nord.nord14 }}',spinner:'{{ .nord.nord15 }}',header:'{{ .nord.nord14 }}'

# ls/exa colors
if not set -q LS_COLORS; and test -f $XDG_DATA_HOME/vivid.dircolors
  read -x LS_COLORS < $XDG_DATA_HOME/vivid.dircolors
end

#
# terminal theme
#

status is-interactive; or exit
status is-login; or test $SHLVL -eq 1; or exit

function hex_octets -a color
  string match -ar '[[:xdigit:]]{2}' $color
end

function hex_color -a color
  string replace '#' '' $color
end

function xrdb_color -a color
  printf 'rgb:%s/%s/%s' (hex_octets $color)
end

set -l ESC \e
set -l BEL \a
set -l ST \e\\

function term_color --inherit-variable ESC --inherit-variable ST -a idx color
  if test $TERM = 'linux'
    printf $ESC']P%x%s' $idx (hex_color $color)
  else if is_iterm
    printf $ESC']P%x%s'$ST $idx (hex_color $color)
  else
    printf $ESC']4;%i;%s'$ST $idx (xrdb_color $color)
  end
end

function term_special --inherit-variable ESC --inherit-variable BEL --inherit-variable ST -a idx color
  if test $TERM = 'linux'
    return
  else if is_iterm
    switch $idx
      case 10; set idx g
      case 11; set idx h
      case 13; set idx m
      case 14; set idx l
      case 17; set idx j
      case 19; set idx k
      case 706; set idx i
      case 708
        set octets (hex_octets $color)
        printf $ESC']6;1;bg;red;brightness;%d'$BEL 0x$octets[1]
        printf $ESC']6;1;bg;green;brightness;%d'$BEL 0x$octets[2]
        printf $ESC']6;1;bg;blue;brightness;%d'$BEL 0x$octets[3]
        return
      case \*; return
    end

    printf $ESC']P%s%s'$ST $idx (hex_color $color)
  else
    printf $ESC']%i;%s'$ST $idx (xrdb_color $color)
  end
end

term_color 0 '{{ .nord.nord1 }}'   # black
term_color 1 '{{ .nord.nord11 }}'  # red
term_color 2 '{{ .nord.nord14 }}'  # green
term_color 3 '{{ .nord.nord13 }}'  # yellow
term_color 4 '{{ .nord.nord9 }}'   # blue
term_color 5 '{{ .nord.nord15 }}'  # magenta
term_color 6 '{{ .nord.nord8 }}'   # cyan
term_color 7 '{{ .nord.nord5 }}'   # white
term_color 8 '{{ .nord.nord3 }}'   # bright black
term_color 9 '{{ .nord.nord11 }}'  # bright red
term_color 10 '{{ .nord.nord14 }}' # bright green
term_color 11 '{{ .nord.nord13 }}' # bright yellow
term_color 12 '{{ .nord.nord9 }}'  # bright blue
term_color 13 '{{ .nord.nord15 }}' # bright magenta
term_color 14 '{{ .nord.nord7 }}'  # bright cyan
term_color 15 '{{ .nord.nord6 }}'  # bright white

term_special 10 '{{ .nord.nord4 }}'  # fg
term_special 11 '{{ .nord.nord0 }}'  # bg
term_special 12 '{{ .nord.nord4 }}'  # cursor (auto)
term_special 13 '{{ .nord.nord0 }}'  # cursor (man) fg
term_special 14 '{{ .nord.nord4 }}'  # cursor (man) bg
term_special 17 '{{ .nord.nord2 }}'  # selection bg
term_special 19 '{{ .nord.nord4 }}'  # selection fg
term_special 706 '{{ .nord.nord4 }}' # bold
term_special 708 '{{ .nord.nord0 }}' # chrome

# clean up
functions -e hex_octets hex_color xrdb_color term_color term_special

# vi: ft=fish
