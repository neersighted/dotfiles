{{- if lookPath "tmux" -}}
# run code on tmux reattach (signaled below)
function __tmux_resync --on-signal USR1
  if is_tmux
    # sync variables defined in tmux's update-environment array
    for entry in (tmux show-environment)
      if string match -rq '^-' -- $entry
        set -eg (string replace '-' '' -- $entry)
      else
        set var (string split '=' $entry)
        set -gx $var[1] $var[2]
      end
    end
  else
    # default is to exit on USR1, which we do when not in TMUX
    exit
  end
end

function __tmux_session_name
  # no tmux in editors
  is_editor; and return 1
  # no nested tmux
  is_tmux; and return 1

  # is a remote login
  if is_ssh
    printf '%s-%s' (prompt_hostname) (string replace -a '.' '-' (string split ' ' $SSH_CONNECTION)[1]); return
  end

  set -q tty; or set -g tty (tty)

  # is a console/tty
  if not string match -rq '^/dev/(pts/\d+|ttys\d+)$' $tty; and not is_wsl1
    printf '%s' $tty; return
  end

  # is a terminal/pty
  if is_wsl
    printf '%s' $WSL_DISTRO_NAME
  else
    printf '%s' (prompt_hostname)
  end
end

function __tmux_auto_launch --on-event fish_startup
  status is-interactive; or return
  set session (__tmux_session_name); or return

  if not tmux has-session -t=$session &>/dev/null
    tmux new -s $session
  else if test -z (tmux list-clients -t $session)[1]
    tmux attach -t $session \; run-shell 'pkill -USR1 -P #{pid} fish'
  end
end
{{- end -}}
