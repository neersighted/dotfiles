#!/usr/bin/env fish

if not command -q rustup
  set_color red
  echo "Installing rustup..."
  set_color normal

  curl -sSf https://sh.rustup.rs | sh -s -- -y --no-modify-path --default-toolchain none
  path_prepend $CARGO_HOME/bin
else
  set_color red
  echo "Updating rustup..."
  set_color normal

  rustup self update
end

for toolchain in stable nightly
  if rustup toolchain list | string match -rq "^$toolchain"
    set_color red
    echo "Updating $toolchain Rust toolchain..."
    set_color normal

    rustup update --no-self-update $toolchain
  else
    set_color red
    echo "Installing $toolchain Rust toolchain..."
    set_color normal

    rustup toolchain install $toolchain
  end
end

set installed (cargo install --list | string match -r '^\S+')

set wanted \
   cargo-audit \
   cargo-asm \
   cargo-binutils \
   cargo-bloat \
   cargo-depgraph \
   cargo-edit \
   cargo-expand \
   cargo-feature \
   cargo-fuzz \
   cargo-release \
   cargo-sort \
   cargo-supply-chain \
   cargo-outdated \
   cargo-udeps \
   cargo-update \
   cargo-watch \
   flamegraph

if is_linux
  set -a wanted \
     cargo-tarpaulin \
     cross
end

for crate in $wanted
  if contains -- $crate $installed
    set i (contains -i -- $crate $wanted)
    set -e wanted[$i]
  end
end

if test (count $wanted) -gt 0
  set_color red
  echo "Installing crates..."
  set_color normal

  cargo install --locked --force -- $wanted
end

if test (count $installed) -gt 0
  set_color red
  echo "Updating crates..."
  set_color normal

  cargo install-update -- $installed
end
