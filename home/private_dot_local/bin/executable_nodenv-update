#!/usr/bin/env fish

set_color green
echo "Updating nodenv..."
set_color normal

for repo in $NODENV_ROOT $NODENV_ROOT/plugins/*
  git -C $repo pull -q
end

set current (chezmoi data | jq -r '.node.current')
set lts (chezmoi data | jq -r '.node.lts.active')

for release in $lts $current
  if test "$release" = "$current"
    set type "Current"
  else
    set type "LTS"
  end

  set latest (nodenv latest --print $release)
  set installed (nodenv latest --print-installed $release)

  if test "$installed" != "$latest"
    set_color green
    echo "Installing latest $type Node..."
    set_color normal

    nodenv latest install $release
    if test $status -eq 0; and test "$release" = "$lts"
      echo "$latest" > $NODENV_ROOT/version
    end
  end
end

set_color green
echo "Updating packages..."
set_color normal

function npm
  command nodenv exec npm $argv
end

for node in (nodenv versions --bare)
  set -lx NODENV_VERSION $node

  for spec in (npm outdated -g --json | jq -c 'to_entries[]')
    set name (jq --argjson spec $spec -nr '$spec | "\(.key)"')
    set current (jq --argjson spec $spec -nr '$spec | "\(.value.current)"')
    set latest (jq --argjson spec $spec -nr '$spec | "\(.value.latest)"')

    set_color green
    echo "Updating $name from $current to $latest ($node)..."
    set_color normal

    npm install -g "$name@$latest"
  end
end
