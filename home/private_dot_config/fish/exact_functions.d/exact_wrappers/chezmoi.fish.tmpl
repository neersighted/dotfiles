{{- if lookPath "chezmoi" -}}
function __chezmoi_targets
  command chezmoi managed --include=files,symlinks,scripts \
    | fzf --multi --preview 'chezmoi cat ~/{}'
end

function chezmoi
  set argc (count $argv)
  switch $argv[1]
  case cat
    if test $argc -eq 1
      if set targets (__chezmoi_targets)
        chezmoi cat '~/'$targets
      end
    else if test $argc -eq 2
      command chezmoi $argv | bat --style=numbers --color=always --file-name $argv[-1]
    else
      command chezmoi $argv
    end
  case cd
    cd (command chezmoi source-path)
  case edit
    if test (count $argv) -eq 1
      if set targets (__chezmoi_targets)
        chezmoi edit '~/'$targets
      end
    else
      # use the git-editor wrapper to take advantage of custom logic
      set -lx VISUAL git-editor

      command chezmoi $argv
    end
  case add
    if test $argc -eq 1
      if set modified (chezmoi status \
        | string match -re '^.M|^M.' | string sub -s 4 \
        | fzf --multi --preview 'chezmoi diff --reverse --color=true ~/{}')
        set targets '~/'$modified
        chezmoi add $modified
      end
    else
      command chezmoi $argv
    end
  case '*'
    command chezmoi $argv
  end
end
{{- end -}}
