{{- if lookPath "chezmoi" -}}

function __chezmoi_targets
  echo '~/'(command chezmoi managed --include=files,symlinks,scripts \
     | fzf --multi --preview 'chezmoi cat ~/{}')
end

function chezmoi
  switch $argv[1]
  case cat
    set argc (count $argv)
    if test $argc -eq 1
      if set targets (__chezmoi_targets)
        commandline --replace "chezmoi cat $targets"
      end
    else if test $argc -eq 2
      command chezmoi $argv | bat --style=numbers --color=always --file-name $argv[-1]
    else
      command chezmoi $argv
    end
  case cd
    cd (command chezmoi source-path)
  case edit
    if test (count $argv) -eq 1
      if set targets (__chezmoi_targets)
        commandline --replace "chezmoi edit $targets"
      end
    else
      # use the git-editor wrapper to take advantage of custom logic
      set -lx VISUAL git-editor

      command chezmoi $argv
    end
  case add
    if test (count $argv) -eq 1
      if set modified (chezmoi status \
        | string match -re '^.M|^M.' | string sub -s 4 \
        | fzf --multi --preview 'chezmoi diff --reverse --color=true ~/{}')
        set args '~/'$modified
        commandline --replace "chezmoi re-add $args"
      end
    else
      command chezmoi $argv
    end
  case '*'
    command chezmoi $argv
  end
end
{{- end -}}
